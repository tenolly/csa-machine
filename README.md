# Лабораторная работа 4
Выполнил: `Пышкин Никита Сергеевич, P3213`<br>
Вариант: `alg | risc | neum | hw | tick | binary | trap | mem | cstr | prob2 | cache`

# Язык программирования
## Описание синтаксиса (Форма Бэкуса-Наура)
```
<программа> ::= <операция> | <операция> <пробельная последовательность> <программа>

<операция> ::= <цикл for> | <условный оператор if> | <операция присвоения> | <операция инициализации>

<пробельная последовательность> ::= (<пробел> | <комментарий>) (<пробельная последовательность> | <пусто>)
<комментарий> ::= "#" <последовательность символов> "\n"

<цикл for> ::= "for" "[" 
    (<операция инициализации> | <операция присвоения> | <пусто>) ";" 
    (<булево выражение> | <пусто>) ";"
    (<операция присвоения> | <пусто>) 
"]" "{" <программа> "}"

<условный оператор if> ::= "if" "(" <булево выражение> ")" "{" <программа> "}"

<операция инициализации> ::= <переменная> ":" <тип данных> "=" (<выражение> | <операция ввода>) ";"
<операция присвоения> ::= <переменная> "=" (<выражение> | <операция ввода>) ";"
<операция ввода> ::= "input" "(" (<число> | <пусто>) ")"
<тип данных> ::= "int" | "str"

<булево выражение> ::= <булево OR>
<булево OR> ::= <булево AND> | <булево OR> "or" <булево AND>
<булево AND> ::= <булево NOT> | <булево AND> "and" <булево NOT>
<булево NOT> ::= <булева единица> | "not" <булево NOT>
<булева единица> ::= "(" <булево выражение> ")" | <выражение> <операция сравнения> <выражение> 

<выражение> ::= <слагаемое> | <слагаемое> ("+" | "-") <слагаемое> 
<слагаемое> ::= <множитель> | <множитель> ("*" | "/" | "%") <множитель>
<множитель> ::= <основа> | <основа> "**" <множитель>
<основа> ::= <переменная> | <число> | <строка> | "(" <выражение> ")"

<операция сравнения> ::= "==" | "<" | "<=" | ">" | ">=" | "!="

<число> ::= <последовательность цифр> | "-" <последовательность цифр>
<последовательность цифр> ::= <цифра> | <цифра> <последовательность цифр>

<строка> ::= '"' (<последовательность символов> | <пусто>) '"'
<последовательность символов> ::= <символ> | <символ> <последовательность символов>
<символ> ::= <цифра> | <буква> | <пробел> | "_"

<переменная> ::= (<буква> | "_") | (<буква> | "_") <название переменной>
<название переменной> ::= <символ переменной> | <символ переменной> <название переменной> 
<символ переменной> ::= <цифра> | <буква> | "_"

<пусто> ::= ε
<пробел> ::= "\n" | "\t" | " "
<цифра> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
<буква> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" 
    | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" 
    | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" 
    | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" 
    | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" 
```

# Организация памяти
Будет позже...

# Система команд
Будет позже...

# Транслятор
Будет позже...

# Модель процессора
Будет позже...
